plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group artifact.group
ext.artifactId = "crypto-android"
version artifact.version

android {
    compileSdk 31

    defaultConfig {
        minSdk 19
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(":crypto-common")

    androidTestImplementation project(":crypto-testlib")
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = project.group
                artifactId = artifactId
                version = project.version

                pom {
                    developers {
                        developer {
                            id = "Izzy"
                            name = "Ismael Alonso"
                        }
                    }
                    scm {
                        connection = "https://github.com/timewheel/crypto-util.git"
                        developerConnection = "https://github.com/timewheel/crypto-util.git"
                        url = "https://github.com/timewheel/crypto-util"
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}
